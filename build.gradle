buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.guardsquare:proguard-gradle:7.3.1'
	}
}

plugins {
	id 'org.cadixdev.licenser' version '0.6.1'
}

apply plugin: 'java'

archivesBaseName = 'unbted'
version = file('version.txt').text.trim()

repositories {
	mavenCentral()
}

dependencies {
	implementation('com.google.guava:guava:31.1-jre') {
		transitive = false
	}
	implementation 'com.google.guava:failureaccess:1.0.1'
	implementation 'com.google.code.gson:gson:2.8.5'
	implementation 'net.sf.jopt-simple:jopt-simple:6.0-alpha-2'
	implementation 'org.jline:jline-terminal:3.21.0'
	implementation 'org.jline:jline-terminal-jansi:3.21.0'
	implementation 'org.jline:jline-reader:3.21.0'
	implementation 'org.jline:jline-builtins:3.21.0'
}

task proguard(type: proguard.gradle.ProGuardTask) {
	dependsOn compileJava
	injars sourceSets.main.compileClasspath.plus(sourceSets.main.output)
	libraryjars files(
			System.properties["java.home"]+"/jmods/java.base.jmod",
			System.properties["java.home"]+"/jmods/java.logging.jmod",
			System.properties["java.home"]+"/jmods/java.management.jmod",
			System.properties["java.home"]+"/jmods/jdk.unsupported.jmod"
		)
	outjars 'build/tmp/proguard.jar'
	
	dontoptimize
	dontobfuscate
	
	keep 'class com.unascribed.nbted.** { *; }'
	keepclassmembers 'class ** { *; }'
	
	dontwarn 'org.checkerframework.**'
	dontwarn 'com.google.errorprone.**'
	dontwarn 'com.google.j2objc.**'
	dontwarn 'javax.annotation.**'
	dontwarn 'org.codehaus.mojo.animal_sniffer.**'
	dontwarn 'org.mozilla.universalchardet.**'
	dontwarn 'java.sql.**' // gson adapters we don't use
	
	verbose
}

task proguardJar(type: Jar) {
	dependsOn proguard
	manifest {
		attributes (
			'Main-Class': 'com.unascribed.nbted.NBTEd'
		)
	}
	from(zipTree('build/tmp/proguard.jar'))
	exclude 'META-INF/services/**'
	exclude 'META-INF/maven/**'
	exclude 'META-INF/native-image/**'
}

jar.enabled = false

license {
	header = file('HEADER')
	matching('**/io/github/steveice10/opennbt/**') {
		header = file('opennbt-LICENSE')
	}
}

tasks.build.dependsOn proguardJar